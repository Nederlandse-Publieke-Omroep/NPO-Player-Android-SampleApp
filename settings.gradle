pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}

def localProperties = new Properties()
localProperties.load(new File(rootDir.absolutePath + "/local.properties").newDataInputStream())

static def getTagPackageCloudKey(Properties properties) {
    def externalModuleDir = properties.getProperty('npotag_package_cloud_key', null)
    if (externalModuleDir == null) {
        throw new GradleException("NPOTag package cloud key not found. Define npotag_package_cloud_key in the local.properties file!")
    }
    return externalModuleDir
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
    repositories {
        google()
        mavenCentral()
        // NPO Player library
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Nederlandse-Publieke-Omroep/npo-player-android"
            credentials {
                username = localProperties.getProperty("github_packages_user", null) ?: System.getenv("GITHUB_ACTOR")
                password = localProperties.getProperty("github_packages_token", null) ?: System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            url 'https://artifacts.bitmovin.com/artifactory/public-releases'
        }
        maven {
            url "https://packagecloud.io/priv/${getTagPackageCloudKey(localProperties)}/npo-data/npotag-v2-android/maven2"
        }
    }
}
rootProject.name = "NPO Player Android SampleApp"
include ':SampleApp:mobile'
include ':SampleApp:shared'
include ':SampleApp:tv'
