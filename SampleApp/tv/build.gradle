plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    namespace 'nl.npo.player.sampleApp.tv'
    compileSdk 35

    Properties localProperties = new Properties()
    File propertiesFile = project.rootProject.file('local.properties')
    if (propertiesFile.exists()) {
        localProperties.load(propertiesFile.newDataInputStream())
    }

    defaultConfig {
        applicationId "nl.npo.player.sampleApp.tv"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "$libVersionName"

        manifestPlaceholders = [
                'YOUR-PLAYER-LICENSE-KEY'   : "${localProperties.your_player_license_key_here}",
                'YOUR-ANALYTICS-LICENSE-KEY': "${localProperties.your_analytics_license_key_here}",
                'appFlavorName'             : "$appName",
                'appFlavorNameShort'        : "$appNameShort"
        ]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("${localProperties.storeFile}")
            storePassword "${localProperties.storePassword}"
            keyAlias "${localProperties.keyAlias}"
            keyPassword "${localProperties.keyPassword}"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig = true
        viewBinding = true
        compose = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    lintOptions {
        // There seems to be an issue in AGP which doesn't recognise the TestDownloadService as being a android.app.Service subclass
        disable "Instantiatable"
    }
}

composeCompiler {
    enableStrongSkippingMode = true
    reportsDestination = layout.buildDirectory.dir("compose_compiler")
    metricsDestination = layout.buildDirectory.dir("compose_compiler")
}

dependencies {
    api project(":SampleApp:shared")

    implementation "nl.npo.player.library:npo-player-android:$libVersionName"
    implementation "nl.npo.player.library:ster-ads:$libVersionName"
    implementation "nl.npo.player.library:presentation-tv:$libVersionName"

    implementation "androidx.core:core-ktx:$androidXCore"
    implementation "androidx.appcompat:appcompat:$androidXAppCompat"
    implementation "androidx.fragment:fragment-ktx:$androidXFragment"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidxLifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycle"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

    // Compose
    implementation 'androidx.compose.ui:ui-android:1.7.7'
    implementation 'androidx.tv:tv-material:1.0.0'

    implementation "com.google.dagger:hilt-android:$androidHilt"
    ksp "com.google.dagger:hilt-compiler:$androidHilt"

    implementation 'androidx.leanback:leanback:1.0.0'

    implementation 'com.github.bumptech.glide:glide:4.15.1'
}
