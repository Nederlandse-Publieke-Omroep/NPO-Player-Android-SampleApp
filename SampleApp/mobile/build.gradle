plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    namespace 'nl.npo.player.sampleApp'
    compileSdk = 36

    Properties localProperties = new Properties()
    File propertiesFile = project.rootProject.file('local.properties')
    if (propertiesFile.exists()) {
        localProperties.load(propertiesFile.newDataInputStream())
    }

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "$libVersionName"

        applicationId "nl.npo.player.sampleApp"
        manifestPlaceholders = [
                'YOUR-PLAYER-LICENSE-KEY'   : "${localProperties.your_player_license_key_here}",
                'YOUR-ANALYTICS-LICENSE-KEY': "${localProperties.your_analytics_license_key_here}",
                'appFlavorName'             : "$appName",
                'appFlavorNameShort'        : "$appNameShort"
        ]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("${localProperties.storeFile}")
            storePassword "${localProperties.storePassword}"
            keyAlias "${localProperties.keyAlias}"
            keyPassword "${localProperties.keyPassword}"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose = true
        buildConfig = true
        viewBinding = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3' // or newer
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    // There seems to be an issue in AGP which doesn't recognise the TestDownloadService as being a android.app.Service subclass
    lint {
        disable 'Instantiatable'
    }

}

dependencies {
    implementation 'androidx.compose.runtime:runtime-livedata:1.9.4'
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.1.5"
    implementation "nl.npo.player.library:npo-player-android:$libVersionName"
    api project(":SampleApp:shared")

    implementation "nl.npo.player.library:ster-ads:$libVersionName"
    implementation "nl.npo.player.library:presentation-mobile:$libVersionName"

    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
    implementation "androidx.core:core-ktx:$androidXCore"
    implementation "androidx.appcompat:appcompat:$androidXAppCompat"
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.fragment:fragment-ktx:$androidXFragment"
    implementation "androidx.navigation:navigation-ui-ktx:2.8.7"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidxLifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycle"
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    ksp "androidx.lifecycle:lifecycle-compiler:$androidxLifecycle"


    // Compose BOM keeps all versions aligned
    implementation platform('androidx.compose:compose-bom:2025.01.00')

    // Core Compose libraries (these are what provide Column/Row/Box/etc.)
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.9.4'
    implementation 'androidx.navigation:navigation-compose'
    implementation 'androidx.compose.material:material-icons-core-android:1.7.8'
    implementation 'androidx.compose.material:material-icons-extended'

    implementation 'androidx.activity:activity-compose:1.9.2'
    implementation 'io.coil-kt:coil-compose:2.6.0'

    // Optional previews and debugging tools
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'



    implementation "com.google.dagger:hilt-android:$androidHilt"
    implementation"androidx.hilt:hilt-navigation-compose:1.2.0"
    ksp "com.google.dagger:hilt-compiler:$androidHilt"

    //only needed if Chromecast is used
    implementation "com.google.android.gms:play-services-cast-framework:22.0.0"
    implementation 'androidx.mediarouter:mediarouter:1.7.0'  //only needed if Chromecast is used

    implementation 'com.github.bumptech.glide:glide:4.15.1'
}
